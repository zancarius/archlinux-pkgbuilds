# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Benjamin A. SHelton <zancarius@gmail.com>
#
# This package may be required for current builds of GitLab.

pkgname=http-parser28
_pkgname=http-parser
pkgver=2.8.1
pkgrel=1
pkgdesc="Parser for HTTP Request/Response written in C"
arch=('x86_64')
url="https://github.com/nodejs/http-parser"
license=('MIT')
depends=('glibc')
source=($_pkgname-$pkgver.tar.gz::"${url}/archive/v$pkgver.tar.gz"
        http-max-header-size.patch) # "${url}/commit/0ae8d93f7335c0279f37b5ca5c26ea881ac17586.patch" + backporting
sha512sums=('6f52f543d979f39688ccefae236527a8183929b3d30f5370570107b01cf89d0338b448249a81102b78d31615d2e8f6e7c708f8961f55ece08e7d3a40e5ad0883'
            '24de54a77860e2d1642bd0e74562a411374967fe6a08913a885b526185089b0a8bb78f25462fec2accfbeb63d249afb2385de3c82f8d9d86bc9d430ede0e7824')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's|-Werror||' Makefile
  patch -p1 -i ../http-max-header-size.patch
}

build() {
  cd $_pkgname-$pkgver
  make library
}

check() {
  cd $_pkgname-$pkgver
  make test
}

package() {
  cd $_pkgname-$pkgver
  #make PREFIX="$pkgdir/usr" install
  mkdir -p "${pkgdir}/usr/lib"
  cp -a "${srcdir}/${_pkgname}-${pkgver}/libhttp_parser.so.2.8.1" "${pkgdir}/usr/lib"
  install -Dm644 LICENSE-MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
